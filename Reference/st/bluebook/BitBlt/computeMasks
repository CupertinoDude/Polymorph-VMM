computeMasks
	| startBits endBits |
	"calculate skew and edge masks"
	destBits <- destForm bits.
	destRaster <- destForm width - 1 // 16 + 1.
	sourceForm notNil
		ifTrue: [sourceBits <- sourceForm bits.
			sourceRaster <- sourceForm width - 1 // 16 + 1].
	halftoneForm notNil
		ifTrue: [halftoneBits <- halftoneForm bits].
	skew <- (sx - dx) bitAnd: 15.
	"how many bits source gets skewed to right"
	startBits <- 16 - (dx bitAnd: 15).
	" how many bits in first word"
	mask1 <- RightMasks at: startBits + 1.
	endBits <- 15 - ((dx + w - 1) bitAnd: 15).
	"how many bits in last word"
	mask2 <- (RightMasks at: endBits + 1) bitInvert.
	skewMask <- 
		(skew = 0
			ifTrue: [0]
			ifFalse: [RightMasks at: 16 - skew + 1]).
	"determine number of words stored per line; merge masks if necessary"
	w < startBits
		ifTrue: [mask1 <- mask1 bitAnd: mask2.
			mask2 <- 0.
			nWords <- 1]
		ifFalse: [nWords <- (w - startBits - 1) // 16 + 2]
